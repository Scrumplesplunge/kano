import foo.bar;  # Import something from foo/bar.kano

var demoint : int32 = 42;
var demobool : bool = 1 == 2 && 3 != 4 || !(5 == 6);
var n : int32 = 1 % -4 + 2 * 3;

# Arrays.
var a : [3]int32 = [3]int32{1, 2, 3};
var a_copy : [3]int32 = a;
var a1 : int32 = a[1];

# Pointers.
var i : int32 = 1;
var iptr : *int32 = &i;
var i2 : int32 = *iptr;

# Variable declarations and aggregate literals.
var x : [2]int32 = [2]int32{1, 2};
var y : bar.mystruct = bar.mystruct{.x = 1, .y = 2};

# Type aliases.
type mytype = [2]int32;
var z : mytype = mytype{1, 2};

# Classes.
class vec2 {
  var x : float;
  var y : float;
  function f() : int32 {
    return 42;
  }
}
var v : vec2 = vec2{.x = 1, .y = 1};

# Functions.
function fib(n : int32) : int32 {
  print(v.f());
  var a : int32 = 0;
  var b : int32 = 1;
  var i : int32 = 0;
  while (i < n) {  # a = fib(i), b = fib(i + 1)
    i = i + 1;     # a = fib(i - 1), b = fib(i)
    b = a + b;     # b = fib(i - 1) + fib(i) = fib(i + 1)
    a = b - a;     # a = fib(i + 1) - fib(i - 1) = fib(i)
  }
  # i == n -> a = fib(n)
  return a;
}
